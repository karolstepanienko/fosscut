import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.10.3'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation libs.ortools
    // Use JUnit test framework.
    testImplementation libs.junit
}

configurations {
    // add a custom configuration
    jarConfiguration
    jarConfiguration.extendsFrom(implementation)
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'com.fosscut.FossCut'
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }

    // add all configuration dependencies to the jar
    from {
        configurations.jarConfiguration.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // add graalvm native-image configuration to jar
    // based on it 'native-image' binary will know what resources to include
    metaInf {
        from 'src/main/resources/native-image.properties'
        if (Os.isFamily(Os.FAMILY_UNIX) && !Os.isFamily(Os.FAMILY_MAC)) {
            from 'src/main/resources/linux-x86-64/'
        }
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            from 'src/main/resources/win32-x86-64/'
        }
        into 'native-image'
    }
}
