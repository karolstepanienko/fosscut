plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.fosscut'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':shared')
    implementation libs.springStarter
    implementation libs.springStarterWeb
    implementation libs.k8sTektonClient
    implementation libs.springDataRedis
    implementation libs.springStarterWebflux
    testImplementation libs.springStarterTest
    testRuntimeOnly libs.junitPlatform
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

// Remove the 'aot' and 'aotTest' source sets that contain automatically
// generated code which introduces a lot of errors in static code analysis
sourceSets {
    aot {
        java {
            setSrcDirs([])
        }
        resources {
            setSrcDirs([])
        }
    }
    aotTest {
        java {
            setSrcDirs([])
        }
        resources {
            setSrcDirs([])
        }
    }
}

task printSourceSets {
    doLast {
        sourceSets.each { set ->
            println "SourceSet '${set.name}':"
            println "  java: ${set.java.srcDirs}"
            println "  resources:    ${set.resources.srcDirs}"
        }
    }
}
